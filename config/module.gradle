def ext = rootProject.ext
def module_name = project.name
def debug = ext.debug[module_name]
if (debug){
    apply plugin: 'com.android.application'
}else {
    apply plugin: 'com.android.library'
}
def id = ext.id[module_name]
android {
    compileSdkVersion ext.android.compileSdkVersion
    defaultConfig {
        applicationId id
        minSdkVersion ext.android.minSdkVersion
        targetSdkVersion ext.android.targetSdkVersion
        versionCode ext.android.versionCode
        versionName ext.android.versionName
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        main {
            if (debug){
                java {
                    srcDir "src/debug/$id"
                }
                manifest.srcFile 'src/debug/AndroidManifest.xml'
            }else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
            }
        }
    }
}
